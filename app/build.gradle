apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.allen.wanandroid"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 100
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi' , 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName=rootProject.getName()+"_v"+defaultConfig.versionName+"_"+releaseTime()+"_"+variant.productFlavors[0].name+".apk"
        }
    }


    flavorDimensions 'store'
    productFlavors {

        // 线上环境
        pro {
            dimension 'store'
            buildConfigField "String", "BASE_URL", "\"http://www.wanandroid.com/\""
            buildConfigField "String", "BUGLY_APP_ID", "\"a1fb27eea0\""
        }

        // 开发阶段
        dev {
            dimension 'store'
            buildConfigField "String", "BASE_URL", "\"http://www.wanandroid.com/\""
            buildConfigField "String", "BUGLY_APP_ID", "\"a1fb27eea0\""
        }

    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNELID: name]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    greendao {
        schemaVersion 1//数据库版本号
        daoPackage 'com.allen.wanandroid.db'//设置DaoMaster、DaoSession、Dao包名
        targetGenDir 'src/main/java'//设置DaoMaster、DaoSession、Dao目录
        //targetGenDirTest：设置生成单元测试目录
        //generateTests：设置自动生成单元测试用例
    }
}



dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(':module_base')
    annotationProcessor 'com.github.bumptech.glide:compiler:4.5.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.hyman:flowlayout-lib:1.1.2'
    implementation 'cn.bingoogolapple:bga-banner:2.2.4@aar'
    implementation 'com.tencent.bugly:crashreport:2.6.6'
    implementation 'com.tencent.bugly:nativecrashreport:3.3.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    // Optional, if you use support library fragments:
    debugImplementation 'com.squareup.leakcanary:leakcanary-support-fragment:1.6.1'

}
